plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.google.protobuf" version "0.8.18"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.18.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.40.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc { option 'lite' }
            }
            task.builtins {
                java { option 'lite' }
                kotlin { option 'lite' }
            }
        }
    }
}

android {
    compileSdk 29

    defaultConfig {
        applicationId "dev.mobile.maestro"
        minSdk 21
        targetSdk 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }
}

sourceSets {
    generated {
        java {
            srcDirs += 'build/generated/source/proto/main/grpc'
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/kotlin'
        }
    }
}

dependencies {
    protobuf project(':maestro-proto')

    implementation 'io.grpc:grpc-kotlin-stub:1.2.1'
    implementation 'io.grpc:grpc-netty-shaded:1.45.0'
    implementation 'io.grpc:grpc-stub:1.45.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.45.0'
    implementation 'io.grpc:grpc-okhttp:1.45.0'
    implementation 'com.google.protobuf:protobuf-kotlin-lite:3.19.4'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
